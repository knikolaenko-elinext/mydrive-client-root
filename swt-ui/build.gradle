buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:1.5.4'
    }
}

configurations {
    win
    linux
}

apply plugin: 'application'
apply plugin: 'edu.sc.seis.launch4j'

dependencies {
    compile project(':core')
    compile 'log4j:log4j:1.2.17'
    if (System.getProperty("os.name").toLowerCase().contains("windows")) {
        compile 'org.eclipse.swt:org.eclipse.swt.win32.win32.x86_64:4.3'
    } else {
        compile 'org.eclipse.swt:org.eclipse.swt.gtk.linux.x86_64:4.3'
    }
    win 'org.eclipse.swt:org.eclipse.swt.win32.win32.x86_64:4.3'
    linux 'org.eclipse.swt:org.eclipse.swt.gtk.linux.x86_64:4.3'
}

mainClassName = "ch.mydrive.Main"

launch4j {
    mainClassName = 'ch.mydrive.Main'
    icon = '../../assets/app-logo.ico'
    outfile = 'mydrive.exe'
}

task installJnlp (dependsOn: installDist) {
    doLast {
        mkdir('build/install/swt-ui-jnlp')
        file('build/install/swt-ui/lib').listFiles().each {
            ant.signjar(jar: it.absolutePath,
                    alias: 'selfsigned',
                    keystore: 'sign/keystore.jks',
                    storepass: 'password',
                    keypass: 'password',
                    signedjar: 'build/install/swt-ui-jnlp/' + it.name);
        }
    }
}
build.dependsOn installJnlp

task installWin (dependsOn: installDist, type: Copy) {
    into 'build/install/swt-ui-win'
    into (""){
        from "build/install/swt-ui"
        exclude "**/org.eclipse.swt*"
    }
    into("lib") {
        from configurations.win
    }
}

task installLinux (dependsOn: installDist, type: Copy) {
    into 'build/install/swt-ui-linux'
    into (""){
        from "build/install/swt-ui"
        exclude "**/org.eclipse.swt*"
    }
    into("lib") {
        from configurations.linux
    }
}

build.dependsOn installWin
build.dependsOn installLinux